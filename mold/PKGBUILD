# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgname=mold
pkgver=2.1.0
pkgrel=1
_commit='a357445654f456a8c0683a2acf14f71a74fcbc6e'
pkgdesc='A Modern Linker'
arch=('loong64' 'x86_64')
url='https://github.com/rui314/mold'
license=('MIT')
# xxhash is bundled
depends=('glibc' 'gcc-libs' 'mimalloc' 'openssl' 'zlib' 'tbb' 'zstd')
makedepends=('git' 'python' 'cmake')
checkdepends=('clang' 'libdwarf')
source=("$pkgname::git+$url.git#commit=$_commit")
b2sums=('SKIP')

build() {
  cmake \
  -S "$pkgname" \
  -B build \
  -D CMAKE_BUILD_TYPE='Release' \
  -D CMAKE_INSTALL_PREFIX='/usr' \
  -D CMAKE_INSTALL_LIBEXECDIR='lib' \
  -D MOLD_USE_SYSTEM_MIMALLOC=ON \
  -D MOLD_USE_SYSTEM_TBB=ON \
  -D MOLD_LTO=ON \
  -D MOLD_USE_MOLD=OFF

  cmake --build build -j$(nproc)
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

